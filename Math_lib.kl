PROGRAM mAth_lib
-------------------------------------------------------------------------------
%ENVIRONMENT REGOPE

ROUTINE kramer(A, D: ARRAY [*,*] OF REAL)  : ARRAY[*,*] OF REAL FROM Matrix_lib
ROUTINE fill_MX(A11, A12, A13, A21, A22, A23, A31, A32, A33 :  REAL)  : ARRAY[*,*] OF REAL FROM Matrix_lib
--=================fill frame for coordinate============================
ROUTINE Fill_PRM(X,Y,Z,w,p,r : REAL): XYZWPR 
VAR
	RES : XYZWPR
	
BEGIN
	RES.X = X
	RES.Y = Y
	RES.Z = Z
	RES.W = W
	RES.P = P
	RES.R = R
	
	RETURN(RES)
END Fill_PRM

--=================distance betwin to point============================
ROUTINE Dist(P1,P2: XYZWPR): REAL 
VAR
	d : REAL

BEGIN
	D=  (P2.z-P1.z)*(P2.z-P1.z) 
	D=  D + (P2.y-P1.y)*(P2.y-P1.y) 
	D=  D + (P2.x-P1.x)*(P2.x-P1.x) 
	D = SQRT (D)
	RETURN(D)
END Dist

--=================centr and radius==================================
ROUTINE Calc_circ(P1,P2,P3,C : XYZWPR; R: REAL )
VAR
 X1,X2,X3, Y1,Y2,Y3, M1,M2 : REAL

BEGIN
	--calculate centr of circle
	X1=P1.X
	X2=P2.X
	X3=P3.X

	Y1=P1.Y
	Y2=P2.Y
	Y3=P3.Y
	
	M1=(Y2-Y1) / (X2-X1)
	M2=(Y3-Y2) / (X3-X2)
	
	C.X=(M1*M2*(y1-y3) + M2*(X1+X2) - M1*(X2+X3)) / (2*(M2-M1)) --X coordinate
		
	C.Y=((X2+X3)/2 - C.X)/M2 + (Y2+Y3)/2 --Y coordinate

	C.Z = 0
	C.W = 0
	C.P = 0
	C.R = 0
	
			
	--calc radius of pipe,
	R = SQRT((C.x-P1.x)*(C.x-P1.x) + (C.y-P1.y)*(C.y-P1.y) )
		

END Calc_circ

--=================centr of mass===========================
ROUTINE CNTR_GRAV(P1,P2,P3,P4: XYZWPR): XYZWPR 
VAR
	Res : XYZWPR

BEGIN
	Res.X = (P1.X + P2.X + P3.X + P4.X)/4
	Res.Y = (P1.Y + P2.Y + P3.Y + P4.Y)/4
	Res.Z = (P1.Z + P2.Z + P3.Z + P4.Z)/4
	Res.W = 0
	Res.P = 0
	Res.R = 0
	
	RETURN(RES)
END CNTR_GRAV


--=================centr of  circl in 3D===========================
ROUTINE CALC_CNTR(P1,P2,P3, C : XYZWPR; R: REAL)
VAR
	TMP_FRM, rP1, rP2, rP3 : XYZWPR

BEGIN
	
		--calculate temporary frame
	TMP_FRM = FRAME(P1,P3,P2)
	
	--calculate realtive coord  rel TMP_FRM
	rP1 = INV(TMP_FRM) : P1
	rP2 = INV(TMP_FRM) : P2
	rP3 = INV(TMP_FRM) : P3
		
	--calculate Circl
	Calc_circ(rP1, rP2, rP3, C, R)
	
	--calc cnt in ORIGIN frame
	C = TMP_FRM : C 

END CALC_CNTR
--=========================

--=================Projection Point to XY of FRM===========================
ROUTINE Proj_XY (Point, FRM: XYZWPR): XYZWPR
VAR
	TMP_FRM: XYZWPR

BEGIN
	TMP_FRM = INV(FRM) : Point
	TMP_FRM. Z = 0
	
	TMP_FRM = FRM : TMP_FRM
	
	RETURN(TMP_FRM)
END Proj_XY
--=========================

--=================Crossing Point of line and plate===========================
ROUTINE Cross_PNT (F_L, F_P: XYZWPR): XYZWPR -- frame line frame plate
VAR
	pl1, pl2, pl3, pl4, pp1, pp2, pp3 , TMP_FRM: XYZWPR
	M1, M2, M3, Ma, Mc : ARRAY [3,3] OF REAL 
	

BEGIN
	-- get two point on Z axis and two point out of Z -axis
	Pl1 = F_L
	Pl2 = F_L : fill_prm(0,0,1000,	0,0,0)
	Pl3 = F_L : fill_prm(0,1000,500,	0,0,0)
	Pl4 = F_L : fill_prm(1000,0,500,	0,0,0)
	
		-- get 3-point on XY-plate
	PP1 = F_P
	PP2 = F_P : fill_prm(1000,0,0,	0,0,0)
	PP3 = F_P : fill_prm(0,1000,0,	0,0,0)
	
	
	Ma = fill_MX(PL1.x,	PL1.y,	PL1.z,
				  PL2.x,	PL2.y,	PL2.z,
				  PL3.x,	PL3.y,	PL3.z)
	
	Mc = fill_MX(1,	0,	0,
				 1,	0,	0,
				 1,	0,	0)
	
	M1 = kramer(Ma, Mc)-- coefficient 1st plate
	
	Ma = fill_MX(PL1.x,	PL1.y,	PL1.z,
				  PL2.x,	PL2.y,	PL2.z,
				  PL4.x,	PL4.y,	PL4.z)
	M2 = kramer(Ma, Mc) -- coefficient 2nd plate
	
	Ma = fill_MX(Pp1.x,	PP1.y,	Pp1.z,
				  Pp2.x,	Pp2.y,	Pp2.z,
				  Pp3.x,	Pp3.y,	Pp3.z)
	M3 = kramer(Ma, Mc) -- coefficient main plate 
	
	Ma = fill_MX(M1[1,1],	M1[2,1],	M1[3,1],
				  M2[1,1],	M2[2,1],	M2[3,1],
				  M3[1,1],	M3[2,1],	M3[3,1])
				  
	M3 = kramer(Ma, Mc) -- cross point
	
	TMP_FRM.X = M3[1,1]
	TMP_FRM.Y = M3[2,1]
	TMP_FRM.Z = M3[3,1]
	TMP_FRM.W = F_P.W
	TMP_FRM.P =  F_P.P
	TMP_FRM.R =  F_P.R
	TMP_FRM.CONFIG_data = F_P.CONFIG_data
	
	RETURN(TMP_FRM)
END Cross_PNT
--=========================

BEGIN
END math_lib 